
// Изменение количества в Товарах
// Пересчет суммы строки и итога
&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ПересчитатьСуммуСтрокиИИтог();
	
КонецПроцедуры

// Изменение цены в Товарах
// Пересчет суммы строки и итога
&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ПересчитатьСуммуСтрокиИИтог();
	
КонецПроцедуры

// При открытии формы
// Пересчет итоговой суммы
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПересчитатьИтоговуюСумму();
	
КонецПроцедуры

// Пересчет суммы в текущей строке Товаров и итога
&НаКлиенте
Процедура ПересчитатьСуммуСтрокиИИтог()
	
	ТекСтрока = Элементы.Товары.ТекущиеДанные;
	
	Если ТекСтрока <> Неопределено Тогда
		ТекСтрока.Сумма = ТекСтрока.Количество * ТекСтрока.Цена;
	КонецЕсли;
	
	ПересчитатьИтоговуюСумму();
	
КонецПроцедуры

// Пересчет итоговой суммы по табличной части Товары
&НаКлиенте
Процедура ПересчитатьИтоговуюСумму()
	
	СуммаИтого = Объект.Товары.Итог("Сумма");
	Объект.СуммаИтого = СуммаИтого;
	Объект.ИтогоСуммаНакладной = Формат(СуммаИтого, "ЧДЦ=2; ЧРГ=' '");
	
КонецПроцедуры

// Подстановка цены при выборе товара
&НаКлиенте
Процедура ТоварыТоварПриИзменении(Элемент)
	
	ТекСтрока = Элементы.Товары.ТекущиеДанные;
	Если ТекСтрока <> Неопределено И ЗначениеЗаполнено(ТекСтрока.Товар) Тогда
		ТекСтрока.Цена = ТоварыТоварПриИзмененииНаСервере(Объект.Дата, ТекСтрока.Товар);
	КонецЕсли;
	
КонецПроцедуры

// Получение цены товара из регистра
&НаСервере
Функция ТоварыТоварПриИзмененииНаСервере(знДата, Товар) 
	
	Отбор = Новый Структура;
	Отбор.Вставить("Товар", Товар); 
	
	ТекЗначение = РегистрыСведений.ЦеныТоваров.ПолучитьПоследнее(знДата, Отбор);
	
	Возврат ТекЗначение.Цена;
	
КонецФункции